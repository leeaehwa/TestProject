apply plugin: 'com.android.application'

String APK_NAME = "JenkinsTest"

def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd_HHmmssS')
    return formattedDate
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "helloandroidstudio.example.com.testproject"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }

    // 요기랑
    signingConfigs {
        release {
            storeFile file("signature/TestKeystore.jks")
            storePassword "testkey"
            keyAlias "testkey"
            keyPassword "testkey"
        }
    }

    // 빌드 타입에 따라 APK 이름을 바꾸기 위해 추가
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def alignedOutputFile = output.outputFile; // 최종 APK
            def unalignedOutputFile = output.packageApplication.outputFile; // 언얼라인드 APK

            // APK_NAME에 버전이름을 붙임
            def newName = APK_NAME + "-" + defaultConfig.versionName;
            // 빌드 타입이 스냅샷이면 데이트타임도 붙임
            if (variant.buildType.name.equals("SNAPSHOT"))
                newName += "-" + getDate();

            // 빌드 타입 이름 추가
            newName += "-" + variant.buildType.name;

            output.outputFile = new File(alignedOutputFile.parentFile, newName + ".apk");
            output.packageApplication.outputFile = new File(unalignedOutputFile.parentFile, newName + "-unaligned.apk")

        }
    }

    buildTypes {
        release {
            debuggable false // debugging 허용 안함
            //zipAlign true  // apk 최적화
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release    // 여기
        }

        SNAPSHOT {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release // 서명 설정
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.1.1'
}
